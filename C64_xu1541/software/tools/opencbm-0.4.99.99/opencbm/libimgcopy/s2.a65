; Copyright 1999 Michael Klein <michael(dot)klein(at)puffin(dot)lb(dot)shuttle(dot)de>
; All rights reserved.
;
; This file is part of OpenCBM
;
; Redistribution and use in source and binary forms, with or without
; modification, are permitted provided that the following conditions are met:
;
;     * Redistributions of source code must retain the above copyright
;       notice, this list of conditions and the following disclaimer.
;     * Redistributions in binary form must reproduce the above copyright
;       notice, this list of conditions and the following disclaimer in
;       the documentation and/or other materials provided with the
;       distribution.
;     * Neither the name of the OpenCBM team nor the names of its
;       contributors may be used to endorse or promote products derived
;       from this software without specific prior written permission.
;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
; IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
; TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
; PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER
; OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
; EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
; PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
; PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
; LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
; NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
; SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;

	*=$0700

TMP = $86

	jmp gts		; get track/sector
	jmp gbyte
	jmp gblk	; receive block
	jmp sbyte	; send byte
	jmp sblk	; send block
	
	lda #$04
i0	bit $1800
	bne i0
	asl
	sta $1800
i1	lda $1800
	bpl i1
	rts

gts	jsr gbyte
	pha
	jsr gbyte
	tay
	pla
	tax
	rts

gblk	jsr gbyte
	sta ($30),y
	iny
	bne gblk
	rts

sblk	lda ($30),y
	jsr sbyte
	iny
	bne sblk
	rts

sbyte	sta TMP
	ldx #$04
write0	lda #$04
	lsr TMP
	rol
	asl
	sta $1800
write1	lda $1800
	bmi write1
	lda #$02
	lsr TMP
	rol
	asl
	sta $1800
write2	lda $1800
	bpl write2
	dex
	bne write0
	lda #$08
	sta $1800
	rts

gbyte	ldx #$04
read0	lda $1800
	bmi read0
	lda $1800
	lsr
	ror TMP
	lda #$10
	sta $1800
read1	lda $1800
	bpl read1
	lda $1800
	lsr
	ror TMP
	lda #$08
	sta $1800
	dex
	bne read0
	lda TMP
	rts

	do_read=*
